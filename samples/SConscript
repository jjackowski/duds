# This file is part of the DUDS project. It is subject to the BSD-style
# license terms in the LICENSE file found in the top-level directory of this
# distribution and at https://github.com/jjackowski/duds/blob/master/LICENSE.
# No part of DUDS, including this file, may be copied, modified, propagated,
# or distributed except according to the terms contained in the LICENSE file.

Import('*')

envsamp = env.Clone()
envsamp.AppendUnique(
	LIBS = 'libboost_program_options${BOOSTTOOLSET}${BOOSTTAG}${BOOSTABI}${BOOSTVER}'
)

envser = envsamp.Clone()
envser.AppendUnique(
	LIBS = 'libboost_serialization${BOOSTTOOLSET}${BOOSTTAG}${BOOSTABI}${BOOSTVER}'
)

envthread = envsamp.Clone()
envthread.AppendUnique(
	LIBS = 'pthread'
)

if not env['Use_GpioDevPort']:
	envser.Append(CPPDEFINES = 'USE_SYSFS_PORT')
	envthread.Append(CPPDEFINES = 'USE_SYSFS_PORT')

imgarc = envsamp.BppiArc('neticons.bppi')
envsamp.Depends(imgarc, tools['bppic'])
imgarcN = envsamp.BppiArc('numberparts.bppi')
envsamp.Depends(imgarcN, tools['bppic'])

# could loop through directories, but be lazy for now
targets = [
	#env.Program('ds1620', Glob('DS1620/*.cpp') + libs),  # broken
	envthread.Program('time', ['time.cpp'] + libs),
	envthread.Program('addressLCD', ['addressLCD.cpp'] + libs),
	envthread.Program('clockLCD', ['clockLCD.cpp'] + libs),
	envser.Program('units', ['units.cpp'] + libs),
	envthread.Program('apds9301', ['apds9301test.cpp'] + libs),
	envthread.Program('am2320', ['am2320test.cpp'] + libs),
	envthread.Program('amg88xx', ['amg88xxtest.cpp'] + libs),
	envthread.Program('ina219', ['ina219test.cpp'] + libs),
	envthread.Program('ina219LCD', ['ina219LCD.cpp'] + libs),
	envthread.Program('isl29125', ['isl29125test.cpp'] + libs),
	envthread.Program('tsl2591', ['tsl2591test.cpp'] + libs),
	envthread.Program('textdisplay', ['textdisplay.cpp'] + libs),
	envthread.Program('pwmbacklight', ['pwmbacklight.cpp'] + libs),
	envthread.Program('st7920', ['st7920.cpp'] + libs),
	envthread.Program('pinconfig', ['pinconfig.cpp'] + libs),
	envsamp.BppiArc('font_4x6.bppi'),  # public domain
	envsamp.BppiArc('font_8x16.bppi'), # GPL 2.0
]
# is there a way to make this implicit when using BppiArc and BppiCpp?
envsamp.Depends(targets[15], tools['bppic'])
envsamp.Depends(targets[16], tools['bppic'])

# These are be needed if image archive files, instead of header files, are
# generated. SCons seems to be catching the dependency from the includes.
envsamp.Depends(targets[1], imgarc)   # addressLCD
envsamp.Depends(targets[2], imgarcN)  # clockLCD
envsamp.Depends(targets[8], imgarcN)  # ina219LCD
envsamp.Depends(targets[13], imgarc)  # st7920

# fonts
#targets += envsamp.BppiArc('font_*.bppi')  # not so easy; lazy

if envthread['Use_Eigen']:
	enveigen = envthread.Clone()
	enveigen.AppendUnique(CPPPATH = '$EIGENINC')
	targets.append(enveigen.Program('orientation', ['orientation.cpp'] + libs))
	#targets.append(enveigen.Program('orientation-LSM9DS1', ['orientation-LSM9DS1.cpp'] + libs))
	#targets.append(enveigen.Program('orientation-LCD', ['orientation-LCD.cpp'] + libs))

Return('targets')
